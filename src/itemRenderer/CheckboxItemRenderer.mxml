<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" 
		   implements="mx.controls.listClasses.IDropInListItemRenderer">
	<mx:Script>
		<![CDATA[
			import mx.events.CollectionEvent;
			import mx.collections.ArrayCollection;
			import mx.controls.dataGridClasses.DataGridListData;
			import mx.controls.DataGrid;
			import mx.controls.listClasses.BaseListData;
			
			private var _listData:BaseListData;
			private var _dataGrid:DataGrid;
			private var _dataField:String;
			
			[Bindable("dataChange")]
			public function get listData():BaseListData
			{
				return _listData;
			}
			public function set listData(value:BaseListData):void {
				_listData = value;
				_dataGrid = value.owner as DataGrid;
				_dataField = (value as DataGridListData).dataField;
			}
			
			override public function set data(value:Object):void {
				super.data = value;
				cb.selected = value[_dataField];
			}
			
			private function onChange():void {
				data[_dataField] = cb.selected;
				var dp:ArrayCollection = _dataGrid.dataProvider as ArrayCollection;
				dp.dispatchEvent(new CollectionEvent(CollectionEvent.COLLECTION_CHANGE));
			}
		]]>
	</mx:Script>
	<mx:CheckBox id="cb" horizontalCenter="0" change="onChange()" />
</mx:Canvas>
