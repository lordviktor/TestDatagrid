<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml"
		   implements="mx.controls.listClasses.IDropInListItemRenderer"
		   creationComplete="onComplete()">
	<mx:Script>
		<![CDATA[
			import mx.events.CollectionEvent;
			import mx.controls.dataGridClasses.DataGridListData;
			import mx.collections.ArrayCollection;
			import mx.controls.DataGrid;
			import mx.controls.dataGridClasses.DataGridColumn;
			import mx.controls.listClasses.BaseListData;
			
			private var _listData:BaseListData;
			private var _dataGrid:DataGrid;
			private var _dataField:String;
			[Bindable] private var _CBselected:Boolean;
			[Bindable] private var _headerLabel:String;
			
			private function onComplete():void {
				cb.setStyle("fontWeight", this.getStyle("fontWeight"));
			}
			
			[Bindable("dataChange")]
			public function get listData():BaseListData
			{
				return _listData;
			}
			
			public function set listData(value:BaseListData):void
			{
				_listData = value;
				_dataGrid = value.owner as DataGrid;
				_dataField = (value as DataGridListData).dataField;
				_headerLabel = value.label;
			}
			
			override public function set data(value:Object):void {
				super.data = value;
				
				var column:DataGridColumn = value as DataGridColumn;
				column.sortable = false;
				
				checkForSelectedItems();
			}
			
			private function checkForSelectedItems():void {
				var dp:ArrayCollection = _dataGrid.dataProvider as ArrayCollection;
				var n:Number = 0;
				for(var i:int=0 ; i < dp.length ; i++){
					if(dp[i][_dataField])
						n++
				}
				if(n == dp.length){
					_CBselected = true;
				}
			}
			
			private function onCBchange(event:Event):void {
				var dp:ArrayCollection = _dataGrid.dataProvider as ArrayCollection;
				
				for(var i:int=0 ; i < dp.length ; i++){
					dp[i][_dataField] = cb.selected;
				}
				
				dp.dispatchEvent(new CollectionEvent(CollectionEvent.COLLECTION_CHANGE));
				_dataGrid.invalidateList();
			}
			
		]]>
	</mx:Script>	

	<mx:CheckBox id="cb" horizontalCenter="0" label="{_headerLabel}"
				 change="onCBchange(event)" selected="{_CBselected}" />
</mx:Canvas>